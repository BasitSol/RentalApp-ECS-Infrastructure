resource "aws_vpc" "main" {
	cidr_block           = var.vpc_cidr
	enable_dns_support   = var.enable_dns_support
	enable_dns_hostnames = var.enable_dns_hostnames

	tags = {
		Name = "${local.name_prefix}-vpc"
	}
}

resource "aws_internet_gateway" "main" {
	vpc_id = aws_vpc.main.id

	tags = {
		Name = "${local.name_prefix}-igw"
	}
}

resource "aws_subnet" "public" {
	for_each = local.public_subnet_map

	vpc_id                  = aws_vpc.main.id
	cidr_block              = each.value
	availability_zone       = local.selected_azs[tonumber(each.key)]
	map_public_ip_on_launch = true

	tags = {
		Name = "${local.name_prefix}-public-${tonumber(each.key) + 1}"
		Tier = "public"
	}
}

resource "aws_subnet" "private" {
	for_each = local.private_subnet_map

	vpc_id                  = aws_vpc.main.id
	cidr_block              = each.value
	availability_zone       = local.selected_azs[tonumber(each.key)]
	map_public_ip_on_launch = false

	tags = {
		Name = "${local.name_prefix}-private-${tonumber(each.key) + 1}"
		Tier = "private"
	}
}

resource "aws_eip" "nat" {
	for_each = aws_subnet.public

	domain = "vpc"

	tags = {
		Name = "${local.name_prefix}-nat-eip-${tonumber(each.key) + 1}"
	}
}

resource "aws_nat_gateway" "main" {
	for_each = aws_subnet.public

	allocation_id = aws_eip.nat[each.key].id
	subnet_id     = each.value.id

	tags = {
		Name = "${local.name_prefix}-nat-${tonumber(each.key) + 1}"
	}

	depends_on = [aws_internet_gateway.main]
}

resource "aws_route_table" "public" {
	for_each = aws_subnet.public

	vpc_id = aws_vpc.main.id

	route {
		cidr_block     = "0.0.0.0/0"
		gateway_id     = aws_internet_gateway.main.id
	}

	tags = {
		Name = "${local.name_prefix}-public-rt-${each.key}"
	}
}

resource "aws_route_table_association" "public" {
	for_each = aws_subnet.public

	subnet_id      = each.value.id
	route_table_id = aws_route_table.public[each.key].id
}

resource "aws_route_table" "private" {
	for_each = aws_subnet.private

	vpc_id = aws_vpc.main.id

	route {
		cidr_block     = "0.0.0.0/0"
		nat_gateway_id = aws_nat_gateway.main[each.key].id
	}

	tags = {
		Name = "${local.name_prefix}-private-rt-${each.key}"
	}
}

resource "aws_route_table_association" "private" {
	for_each = aws_subnet.private

	subnet_id      = each.value.id
	route_table_id = aws_route_table.private[each.key].id
}
