resource "aws_sns_topic" "alerts" {
	name = "${local.name_prefix}-alerts"
}

resource "aws_sns_topic_subscription" "email" {
	count = var.alert_email != "" ? 1 : 0

	topic_arn = aws_sns_topic.alerts.arn
	protocol  = "email"
	endpoint  = var.alert_email
}

resource "aws_cloudwatch_metric_alarm" "alb_target_5xx" {
	count = var.enable_alerts ? 1 : 0

	alarm_name          = "${local.name_prefix}-alb-target-5xx"
	comparison_operator = "GreaterThanThreshold"
	evaluation_periods  = 2
	metric_name         = "HTTPCode_Target_5XX_Count"
	namespace           = "AWS/ApplicationELB"
	period              = 60
	statistic           = "Sum"
	threshold           = 10
	alarm_description   = "High target 5xx errors on API target group"
	alarm_actions       = [aws_sns_topic.alerts.arn]

	dimensions = {
		LoadBalancer = var.alb_arn_suffix
		TargetGroup  = var.api_target_group_arn_suffix
	}
}

resource "aws_cloudwatch_metric_alarm" "alb_latency" {
	count = var.enable_alerts ? 1 : 0

	alarm_name          = "${local.name_prefix}-alb-latency"
	comparison_operator = "GreaterThanThreshold"
	evaluation_periods  = 3
	metric_name         = "TargetResponseTime"
	namespace           = "AWS/ApplicationELB"
	period              = 60
	statistic           = "Average"
	threshold           = 2
	alarm_description   = "High API response latency on ALB"
	alarm_actions       = [aws_sns_topic.alerts.arn]

	dimensions = {
		LoadBalancer = var.alb_arn_suffix
		TargetGroup  = var.api_target_group_arn_suffix
	}
}

resource "aws_cloudwatch_metric_alarm" "ecs_cpu_high" {
	count = var.enable_alerts ? 1 : 0

	alarm_name          = "${local.name_prefix}-ecs-cpu-high"
	comparison_operator = "GreaterThanThreshold"
	evaluation_periods  = 3
	metric_name         = "CPUUtilization"
	namespace           = "AWS/ECS"
	period              = 60
	statistic           = "Average"
	threshold           = 85
	alarm_description   = "High CPU utilization on ECS API service"
	alarm_actions       = [aws_sns_topic.alerts.arn]

	dimensions = {
		ClusterName = var.ecs_cluster_name
		ServiceName = var.ecs_service_name
	}
}

resource "aws_budgets_budget" "monthly" {
	count = var.budget_alert_email != "" ? 1 : 0

	name         = "${local.name_prefix}-monthly-budget"
	budget_type  = "COST"
	limit_amount = tostring(var.monthly_budget_limit_usd)
	limit_unit   = "USD"
	time_unit    = "MONTHLY"

	notification {
		comparison_operator        = "GREATER_THAN"
		threshold                  = 80
		threshold_type             = "PERCENTAGE"
		notification_type          = "ACTUAL"
		subscriber_email_addresses = [var.budget_alert_email]
	}

	notification {
		comparison_operator        = "GREATER_THAN"
		threshold                  = 100
		threshold_type             = "PERCENTAGE"
		notification_type          = "FORECASTED"
		subscriber_email_addresses = [var.budget_alert_email]
	}
}
